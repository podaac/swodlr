plugins {
    id 'org.springframework.boot' version '2.7.2'
    id 'io.spring.dependency-management' version '1.0.12.RELEASE'
    id 'application'
    id 'jacoco'
    id 'checkstyle'
    id 'com.google.cloud.tools.jib' version '3.3.0'
}

group = 'gov.nasa.podaac.swodlr'
version = '0.0.1-alpha0'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

checkstyle {
    toolVersion '10.3.4'
    configFile = file('/google_checks.xml')
    configProperties = [
        'org.checkstyle.google.suppressionfilter.config': "$projectDir/config/checkstyle/suppressions.xml"
    ]
    maxWarnings = 0
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-graphql'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.postgresql:postgresql:42.5.0'
    implementation 'com.zaxxer:HikariCP:5.0.1'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.graphql:spring-graphql-test'
    testImplementation 'com.h2database:h2:2.1.214'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('check').configure {
    dependsOn(tasks.named('jacocoTestReport'))
}

tasks.named('jib').configure {
    from {
        image = 'eclipse-temurin:17-jre-alpine'
    }
    to {

        image = "ghcr.io/podaac/swodlr-api:${version}"
        auth {
            username = System.env.GITHUB_ACTOR
            password = System.env.GITHUB_TOKEN
        }
    }
    container {
        labels = [
            'org.opencontainers.image.source': 'https://github.com/podaac/swodlr',
            'org.opencontainers.image.licenses': 'Apache-2.0'
        ]
    }
}
